#
# $Id: configure.in,v 1.1.1.1 2002/01/18 03:16:22 mavrik Exp $
#
dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/ftimes.c)
AC_CONFIG_HEADER(src/config.h)

AC_PREFIX_DEFAULT(/usr/local/integrity)
cgidir='${exec_prefix}/cgi'
docdir='${exec_prefix}/doc'
etcdir='${exec_prefix}/etc'
logdir='${exec_prefix}/log'
rundir='${exec_prefix}/run'
AC_SUBST(cgidir)
AC_SUBST(docdir)
AC_SUBST(etcdir)
AC_SUBST(logdir)
AC_SUBST(rundir)

AC_CANONICAL_SYSTEM

dnl Checks for programs.
AC_PROG_CC
if test "$GCC" = "yes"; then CFLAGS="$CFLAGS -Wall"; fi
AC_PROG_INSTALL
AC_PROG_MAKE_SET

case "$target_os" in

aix*)
        AC_DEFINE(FTimes_UNIX)
        AC_DEFINE(FTimes_AIX)
        AC_DEFINE(USE_AP_SNPRINTF)
        ap_srcs=ap_snprintf.c
        ap_incs=ap_snprintf.h
        AC_SUBST(ap_srcs)
        AC_SUBST(ap_incs)
        ;;

*bsd*)
        AC_DEFINE(FTimes_UNIX)
        AC_DEFINE(FTimes_BSD)
        ;;

linux*)
        AC_DEFINE(FTimes_UNIX)
        AC_DEFINE(FTimes_LINUX)
        ;;

solaris*)
        AC_DEFINE(FTimes_UNIX)
        AC_DEFINE(FTimes_SOLARIS)
        AC_DEFINE(USE_AP_SNPRINTF)
        ap_srcs=ap_snprintf.c
        ap_incs=ap_snprintf.h
        AC_SUBST(ap_srcs)
        AC_SUBST(ap_incs)
        ;;

esac

AC_MSG_CHECKING(whether to compile with XMagic)
AC_ARG_ENABLE(xmagic,
  [  --enable-xmagic         Enable XMagic support ],

  [ AC_MSG_RESULT(yes)
    AC_DEFINE(USE_XMAGIC)
    xmagic_srcs=xmagic.c
    xmagic_incs=xmagic.h
    AC_SUBST(xmagic_srcs)
    AC_SUBST(xmagic_incs) ],

  [ AC_MSG_RESULT(no) ]
)

AC_MSG_CHECKING(whether to compile with SSL)
AC_ARG_ENABLE(ssl,
  [  --enable-ssl            Enable SSL support ],

  [ AC_MSG_RESULT(yes)
    AC_DEFINE(USE_SSL)
      dnl Check for SSL directories.
      AC_MSG_CHECKING([for SSL directory])
      for dir in /usr/local/ssl /usr/lib/ssl /usr/pkg /usr/local /usr; do
          ssldir="$dir"
          if test -f "$dir/include/openssl/ssl.h"; then
              AC_DEFINE(HAVE_OPENSSL)
              break
          fi
          if test -f "$dir/include/ssl.h"; then
              break
          fi
      done
      AC_MSG_RESULT($ssldir)
      AC_SUBST(ssldir)
      AC_DEFINE_UNQUOTED(ssldir, "$ssldir")
      ssl_srcs=ssl.c
      ssl_incs=ssl.h
      AC_SUBST(ssl_srcs)
      AC_SUBST(ssl_incs)
      sslincflags="-I$ssldir/include"
      AC_SUBST(sslincflags)
      ssllibflags="-L$ssldir/lib -lssl -lcrypto"
      AC_SUBST(ssllibflags)
      
      dnl Check for RSAref library.
      AC_MSG_CHECKING([for RSAref library])
      saved_LIBS="$LIBS"
      LIBS="$saved_LIBS -lRSAglue -lrsaref"
      AC_TRY_LINK([], [], [AC_MSG_RESULT(yes); ], [AC_MSG_RESULT(no)]; LIBS="$saved_LIBS")
  ],

  [ AC_MSG_RESULT(no) ]
)

dnl Checks for standard libraries.
AC_CHECK_LIB(nsl, gethostbyname)
AC_CHECK_LIB(socket, socket)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h sys/time.h unistd.h unistd.h sys/select.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_STRUCT_ST_RDEV
AC_HEADER_TIME
AC_TYPE_UID_T

dnl Checks for library functions.
AC_FUNC_MEMCMP
AC_FUNC_STRFTIME
AC_CHECK_FUNCS(gettimeofday socket strerror strstr uname snprintf vsnprintf)

AC_OUTPUT(Makefile cgi/Makefile doc/Makefile etc/Makefile src/Makefile)
